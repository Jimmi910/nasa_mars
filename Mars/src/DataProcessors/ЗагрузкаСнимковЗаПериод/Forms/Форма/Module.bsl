// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АпиКлюч = Константы.АпиКлюч.Получить();
	АпиКлючЗаполнен = Не ПустаяСтрока(АпиКлюч);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не АпиКлючЗаполнен Тогда
		ОткрытьФорму("ОбщаяФорма.ФормаКонстант");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните api ключ'"));
		Отказ = Истина;
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ЗаполнитьЗначенияСвойств(Объект, Период);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьСнимкиЗаПериод(Команда)
	
	ПустаяДата = Дата("00010101");
	Если Объект.ДатаНачала = ПустаяДата 
		Или Объект.ДатаОкончания = ПустаяДата
		Или (Объект.ДатаОкончания < Объект.ДатаНачала) Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не верно указан период'"), "Период", "Объект");
		Возврат;
		
	КонецЕсли;
	КлючФоновогоЗадания = Строка(Новый УникальныйИдентификатор());
	АдресРезультата = ПоместитьВоВременноеХранилище(ПустаяДата, УникальныйИдентификатор);
	
	ЗагрузитьСнимкиЗаПериодСервер(Объект.ДатаНачала, Объект.ДатаОкончания, КлючФоновогоЗадания, АдресРезультата);
	
	Элементы.ГруппаЗагрузки.Видимость = Истина;
	ПодключитьОбработчикОжидания("ОжиданиеЗавершенияЗагрузкиСнимков", 5);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Загрузить снимки за период сервер.
// 
// Параметры:
//  ДатаНачала - Дата - Дата начала
//  ДатаОкончания - Дата - Дата окончания
//  Ключ - Строка - Ключ
//  АдресРезультата - Строка - Адрес результата
&НаСервереБезКонтекста
Процедура ЗагрузитьСнимкиЗаПериодСервер(ДатаНачала, ДатаОкончания, Ключ, АдресРезультата)
		
	ПараметрыЗадания = Новый Массив;
	ПараметрыЗадания.Добавить(ДатаНачала);
	ПараметрыЗадания.Добавить(ДатаОкончания);
	ПараметрыЗадания.Добавить(АдресРезультата);
	
	ИмяЗадания = "Загрузка снимков марсаходов за период с " + Строка(ДатаНачала) + " по " + Строка(ДатаОкончания);
	
	ФоновыеЗадания.Выполнить("МодульФоновыхЗаданийСервер.ЗагрузкаСнимковЗаПериод",
		ПараметрыЗадания, Ключ, ИмяЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжиданиеЗавершенияЗагрузкиСнимков() Экспорт
	
	РезультатФоновойЗагрузки = РезультатВыполненияФоновойЗагрузки(КлючФоновогоЗадания);
	Если РезультатФоновойЗагрузки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатФоновойЗагрузки.ВсегоСнимков > 0 Тогда
		Элементы.ИндикаторЗагрузки.МаксимальноеЗначение = РезультатФоновойЗагрузки.ВсегоСнимков;
		ИндикаторЗагрузки = РезультатФоновойЗагрузки.Обработано;

		Элементы.ИндикаторЗагрузки.Подсказка = "Обработано " + РезультатФоновойЗагрузки.Обработано + " из "
			+ РезультатФоновойЗагрузки.ВсегоСнимков;
	КонецЕсли;
	
	Если РезультатФоновойЗагрузки.Завершено Тогда
		
		ОтключитьОбработчикОжидания("ОжиданиеЗавершенияЗагрузкиСнимков");
		
		Если ПустаяСтрока(РезультатФоновойЗагрузки.ТекстОшибки) Тогда
			Элементы.БубликОжидания.Картинка = БиблиотекаКартинок.ОформлениеФлажок;
		Иначе
			Элементы.БубликОжидания.Картинка = БиблиотекаКартинок.ОформлениеКрест;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(РезультатФоновойЗагрузки.ТекстОшибки);
		КонецЕсли;
		
		РезультатЗагрузки = ПолучитьИзВременногоХранилища(АдресРезультата);
		
	КонецЕсли;
	
КонецПроцедуры

// Результат выполнения фоновой загрузки.
// 
// Параметры:
//  Ключ - Строка - Ключ ФЗ
// 
// Возвращаемое значение:
//  Структура - Результат выполнения фоновой загрузки:
// * ВсегоСнимков - Число - Всего снимков загружается
// * Обработано - Число - уже загружено
// * Завершено - Булево - завершено ФЗ или нет
// * ТекстОшибки - Строка - текст ошибки ФЗ 
&НаСервереБезКонтекста
Функция РезультатВыполненияФоновойЗагрузки(Ключ)
	
	Результат = Обработки.ЗагрузкаСнимковЗаПериод.СтруктураПроцессаОбработкиДанных();
	
	Отбор = Новый Структура("Ключ", Ключ);
	МассивЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	
	Если МассивЗаданий.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Задание = МассивЗаданий[0];
	
	Если Задание.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
		Результат.Завершено = Истина;
	ИначеЕсли Задание.Состояние = СостояниеФоновогоЗадания.ЗавершеноАварийно Тогда
		Результат.Завершено = Истина;
		Результат.ТекстОшибки = Задание.ИнформацияОбОшибке.Описание;
	Иначе
		Результат.Завершено = Ложь;
	КонецЕсли;
	
	МассивСообщений = Задание.ПолучитьСообщенияПользователю(Истина);
		
	Для Каждого Сообщение Из МассивСообщений Цикл
		
		ПолученаяСтруктура = ЗначениеИзСтрокиВнутр(Сообщение.Текст);
		ЗаполнитьЗначенияСвойств(Результат, ПолученаяСтруктура);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти